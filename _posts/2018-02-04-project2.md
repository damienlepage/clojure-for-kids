---
layout: post
title:  "Project 2 - Rock Paper Scissors"
---

# Keywords

Before starting our second project, we will discover two data structures that will be very useful, the first one is keywords. They are a bit like strings that we saw in project 1, except that they have a meaning for the program instead of containing arbitrary text that the program doesn't understand. In other words, they're useful for the programmer more than for the user of the program (or the player of the game in our case). 

A keyword is a word starting with a colon, like `:pizza`, or a few words without a space between, like `:spinach-salad`. In NightCode, go to `Home` and try writing some keywords in the REPL to see what happens.

```clojure
user=> :blue
:blue
user=> :funny-little-kitten
:funny-little-kitten
```

As you can see, keywords evaluate to themselves. This may look a little useless at this point but hey, at least they don't cause an error. Keywords are self-defined like numbers or strings and unlike symbols that we saw in project 1. Dont worry if you find this confusing, we'll discover some interesting properties of keywords later.

# Maps

We will also need to use maps in this project, maps are convenient data structures that allow to arrange data into key/value pairs. The syntax to define a map is curly braces `{}`. Any kind of data can be used inside maps as key or as value, but a type that works very well as a key is the keyword that we saw previously.

Let's have a REPL session to play with maps.

```clojure
user=> (def m {:type :hero :name "Batman" :age 32 :friend {:name "Robin" :age 15}})
#'user/m
user=> m
{:type :hero, :name "Batman", :age 32, :friend {:name "Robin", :age 15}}
```

Notice how we used different data types in this map values: keyword, string, number and even another map. We only used keywords as keys but it's not a requirement and we could happily mix other types if we wanted to.

Also notice how a comma gets added between each pair when displaying a map, this is just to improve readability, you can also write maps with commas if you like it better this way. In effect, commas and spaces are just ignored by the program. 

Let's continue.

```clojure
user=> (get m :name)
"Batman"
user=> (:name m)
"Batman"
user=> (:name (:friend m))
"Robin"
```

So `get` is a function that allows to query a value by its key from a map, but as you can see with the second expression, an interesting property of keywords is that they can also be used as functions when querying maps, kind of a shortcut. The concision becomes even more valuable when dealing with nested maps like in the last example.

There's a lot more to learn about maps but that should be enough to get us started.

# Creating the project

Our second project will be the well-known Rock Paper Scissors game. You can start by opening NightCode and creating a new console project, just like we did in the 
[first project]({{ site.baseurl }}{% link _posts/2018-01-14-project1.md %}). 

> ![New Project]({{ "/assets/caution.png" | absolute_url }}) Never use any space in your project name, "rock paper scissors" would cause you some problems, but dashes are fine instead like in "rock-paper-scissors".

Once you have created it, you can start your project with `Run with REPL` and we'll start building our new game from there.

# Defining our data structure

Our game will ask the player to enter their choice, so we need a way to retrieve an element (rock, paper or scissors) from a user input. To make it simple for the player, we'll only ask them to enter the first letter of the element 'r', 'p' or 's'.

You may remember from the first project, that we can easily capture user input as string with the function `read-line` so it makes sense for our map to use the strings "r", "p" and "s" as keys. Let's start with just putting the full names as values.

Create the following map in `core.clj` and reload the file into the REPL:

```clojure
(def elements {"r" "rock"
               "p" "paper"
               "s" "scissors"})
```

Now try retrieving an element at the REPL:

<div class="solution closed"></div>
```clojure
rock-paper-scissors.core=> (get elements "p")
"paper"
```

Good. Now write a function `get-player-choice` that will ask the user to enter their choice and return the corresponding value from the map.

You already know everything you need for that from the first project. 

Ready? Reload the file and try your function at the REPL. Does it work? If it does then it probably looks something like that:

<div class="solution closed"></div>
```clojure
(defn get-player-choice []
  (println "Rock [r] Paper [p] or Scissors [s]?")
  (get elements (read-line)))
```

Remember that your function needs to come after the definition of `elements` in your file because it uses it.

Let's try it:

```clojure
rock-paper-scissors.core=> (get-player-choice)
Rock [r] Paper [p] or Scissors [s]?s
"scissors"

rock-paper-scissors.core=> (get-player-choice)
Rock [r] Paper [p] or Scissors [s]?1
nil
````

Hmmm ... can you modify this function to ask again when the player enters an invalid choice? 

We learned something called recursion in the first project, that should come handy.

The special form `let` should also be useful here.

Do you have it?

Here is a possible solution:

<div class="solution closed"></div>
```clojure
(defn get-player-choice []
  (println "Rock [r] Paper [p] or Scissors [s]?")
  (let [choice (get elements (read-line))]
    (if (= nil choice)
      (get-player-choice)
      choice)))
```

Does it make sense? If we can't find the element, we saw above that `get` returns nil. If that's the case, we just call the same function again.

Does it work?

```clojure
rock-paper-scissors.core=> (get-player-choice)
Rock [r] Paper [p] or Scissors [s]?a
Rock [r] Paper [p] or Scissors [s]?1
Rock [r] Paper [p] or Scissors [s]?r
"rock"
```

Perfect. 

# Making our code better

Often in programming, we come up with a solution that works but there's a way to refine it and make it better. Let's learn a couple of tricks to simplify our last function.

The first one has to do with the `if` block. Let's look up some documentation about `if`. You can do it as we saw in project 1 but or there's also on inline documentation available in the REPL with the function `doc`:

```clojure
rock-paper-scissors.core=> (doc if)
-------------------------
if
  (if test then else?)
Special Form
  Evaluates test. If not the singular values nil or false,
  evaluates and yields then, otherwise, evaluates and yields else. If
  else is not supplied it defaults to nil.

  Please see http://clojure.org/special_forms#if
nil
```

Ok, so `if` will behave the same whenever the test expression returns `false` or `nil`, so instead of `(= nil choice)` in our function we can simply use `choice`.

<div class="solution closed"></div>
```clojure
(defn get-player-choice []
  (println "Rock [r] Paper [p] or Scissors [s]?")
  (let [choice (get elements (read-line))]
    (if choice
      (get-player-choice)
      choice)))
```

Slightly shorter, and shorter code is better most of the time.

This first trick unlocks a second one, which combines the forms `if` and `let` into a single one called `if-let`. You can look this one up into the documentation too

Here is what it looks like for our function:

<div class="solution closed"></div>
```clojure
(defn get-player-choice []
  (println "Rock [r] Paper [p] or Scissors [s]?")
  (if-let [choice (get elements (read-line))]
    choice
    (get-player-choice)))
```

Pretty cool, no?

# Modeling the relationships

One important piece of information that our program is missing is which element beats which. So far, our map values only contain the full name of the elements, but if you remember our paragraph on maps, a value can also be another map, that we call a nested map.

Let's change the values of the `elements` map to contain 2 pieces of information, the name of the element and what it beats. This is a good opportunity to use keywords.

Do you see where this is going? Give it a try yourself.

Here is a solution:

<div class="solution closed"></div>
```clojure
(def elements {"r" {:name "rock" :beats "scissors"}
               "p" {:name "paper" :beats "rock"}}
               "s" {:name "scissors" :beats "paper"})
```

Now reload your file, and try the `get-player-choice` in the REPL again. You'll see that it still works and now returns all the information that we need.

# Making the computer choose

We need to make the program choose an element randomly in order to be able to play against the computer. Let's make a function called `get-cpu-choice` (CPU is short for Central Processing Unit and is often used in games to refer to computer's controlled character).

We need the program to pick one of the 3 values of our map. The first step is to get a list of these values as we don't need the keys here.

Is there a Clojure function that returns all the values of a map? The answer is yes, do you want to try and find it in the documentation? There's a whole section about Maps, can you find it?

![Maps documentation]({{ "/assets/maps-doc.png" | absolute_url }})

Which row of this table is most likely to contain what we want? 

That's right `Examine` sounds like what we're trying to do here.

Can you find the function that returns all the values now?

Try it in the REPL.

<div class="solution closed"></div>
```clojure
rock-paper-scissors.core=> (vals elements)
({:name "rock", :beats "scissors"} {:name "paper", :beats "rock"} {:name "scissors", :beats "paper"})
```

Now we just need the program to pick one of these 3 elements randomly.

The documentation of `vals` says that it "Returns a sequence of the map's values", there is a huge section in the documentation about Sequences. Let's find the sub-section whose title is `Using a Seq`. The first row `Extract item` seems pretty useful, can you spot a function that is likely to help us?

![Using a Seq]({{ "/assets/using-a-seq.png" | absolute_url }})

That's right, there's a function that returns a random element of a sequence. Try it in the REPL.

Now you have everything you need to build `get-cpu-choice`.

<div class="solution closed"></div>
```clojure
(defn get-cpu-choice []
  (rand-nth (vals elements)))
```

# Comparing two choices

The next and almost final step in building this game is to build a function that can compare two choices.

There is actually nothing new for you to learn here, so this is a great opportunity to test your skills.

Can you build a function `battle` that takes 2 arguments `element1` and `element2`, displays what they are, for instance "paper versus rock", then displays who wins.

Here is a solution for this.

<div class="solution closed"></div>
```clojure
(defn battle [element1 element2]
  (let [name1 (:name element1)
        name2 (:name element2)]
    (println name1 "versus" name2)   
    (cond 
      (= (:beats element1) name2) (println name1 "wins")
      (= (:beats element2) name1) (println name2 "wins")
      :defaut (println "tie"))))
```

# Make it a game

The last part is also straight forward, we need a function `play-game` that gets the player's choice, the computer's choice and then run the battle.

When it's done, we can keep the game running for another round by recursively calling `play-game`.

Here we go.

<div class="solution closed"></div>
```clojure
(defn play-game [] 
  (battle (get-player-choice) (get-cpu-choice))
  (play-game))
    
(defn -main []
  (play-game))
```

Congratulations, you've programmed a game of Rock Paper Scissors!

# Bonus exercise

Instead of simply printing the winner, try making the battle function also return a score, either `1` when the first element wins, `-1` when the second element wins or `0` when it's a tie. Then keep track of the player's score in the `play-game` function and display it after each round.
