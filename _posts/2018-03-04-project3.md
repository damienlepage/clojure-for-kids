---
layout: post
title:  "Project 3 - Guess the word"
---

# The project

In our third project, the player will need to propose letters to try and find the name of a country. The game can be modified to play with cities or animals or common words or any other list of words that you may be able to find.  

![Demo]({{ "/assets/project3.png" | absolute_url }})

# Characters

Strings are composed of characters, nothing surprising with that but how do we represent a single character outside of a string? The answer is to prefix it with `\`. And in order to transform a string into a sequence of characters, the function `seq` can be used. Here is a REPL session to demo this:

```
user=> \a
\a
user=> (seq "this is wonderful")
(\t \h \i \s \space \i \s \space \w \o \n \d \e \r \f \u \l)
```

Notice how the space character is fully spelled out as a character.

# Sets

Another data structure that we haven't seen yet will be very useful for this project, in order to keep track of the letters proposed by the player. This is a type of collection called a set, which is very good at working with unique elements. Its notation is `#{}`.

Let's have a REPL session to discover its properties.

```clojure
user=> #{1 2 3}
#{1 3 2}
user=> (#{1 2 3} 3)
3
user=> (#{1 2 3} 4)
nil
user=> (conj #{1 2 3} 4)
#{1 4 3 2}
user=> (into #{} "hello")
#{\e \h \l \o}
```

As you can see the order of the elements is not maintained. Also, the set can be used as a function to look-up an element it contains, you'll see later how it can be useful (hint: remember the test conditions that are not `false` or `nil`?). Finally, note the useful functions `conj` to add an element into a set and `into` to load another collection (here a string) into a set. Notice how the character `\l` appears only once in the set.

# Reveal or hide a letter

Time to create the project, follow the same procedure as previously and remember to not use any space in your project name. Then let's start writing some code inside your new `core.clj`.

The first part of the project is to create a function called `reveal-letter` that will take 2 arguments, the argument `letter` which is a character, and the argument `known-letters` which is a set of characters already played. This function will return the character `letter` if it's a known one, or replace it with `_` if it's unknown. Here is a REPL session to illustrate:

```clojure
guess-the-word.core=> (reveal-letter \a #{})
\_
guess-the-word.core=> (reveal-letter \b #{\a \b \c})
\b
guess-the-word.core=> (reveal-letter \z #{\a \b \c})
\_
```

Try writing this function yourself then check the solution if needed.

<div class="solution closed"></div>
```clojure
(defn reveal-letter [letter known-letters]
  (if (known-letters letter)
    letter
    \_))
```

Good, however this function hides special characters such as spaces. We only want letters from the alphabet to be replaced. Can you do that? Here are two tips:

* An easy way to get the alphabet into a set is to write it into a string before loading it into a set.
* The function `not` is useful to test the opposite of something.

Now give it a shot and try not to peak at the solution before you have something working.

<div class="solution closed"></div>
```clojure
(def alphabet (into #{} "abcdefghijklmnopqrstuvwxyz"))

(defn reveal-letter [letter known-letters]
  (cond
    (known-letters letter) letter
    (not (alphabet letter)) letter
    :default \_))
```

# Reveal parts of a word

The next step consists in processing an entire word with a function that we can call `reveal-letters`. Here is how it's going to be used:

```clojure
guess-the-word.core=> (reveal-letters "fantastic" #{\a \t \g \y \c})
"_a_ta_t_c"
```

You probably guessed already that this function `reveal-letters` will need to call the previous function `reveal-letter` for each letter of the word. Remember that a string is just a collection of characters and some functions exist to extract items from a collection, we are going to discover 3 of them with a small REPL session:

```clojure
guess-the-word.core=> (first "word")
\w
guess-the-word.core=> (rest "word")
(\o \r \d)
guess-the-word.core=> (next "word")
(\o \r \d)
guess-the-word.core=> (rest "a")
()
guess-the-word.core=> (next "a")
nil
```

Unsurprisingly, `first` returns the first character of the string. What's the difference between `rest` and `next`? There doesn't seem to be any when called with a word that has multiple letters, but there is a major difference when called with a 1-letter word. The fact that `next` returns nil instead of an empty collection makes it a function of choice when we want to combine it with a test expression, see for yourself:

```clojure
guess-the-word.core=> (if (not (empty (rest "a"))) "there is more" "this is the last letter")
"this is the last letter"
guess-the-word.core=> (if (next "a") "there is more" "this is the last letter")
"this is the last letter"
```

After splitting and replacing letters as needed, we also need to put them back together. This is can be done very simply with the function `str`:

```clojure
guess-the-word.core=> (str \b "_zz")
"b_zz"
```

With this knowledge in mind, can you create the function `reveal-letters`? Remember to use `let` to keep track of intermediate results.

<div class="solution closed"></div>
```clojure
(defn reveal-letters [word known-letters]
  (let [letter (first word)
        more (next word)
        current-letter (reveal-letter letter known-letters)]
    (if more
      (str current-letter (reveal-letters more known-letters))
      current-letter)))
```

The solution above can be improved by using a nice feature of the Clojure language which is called destructuring. This is just a trick to save some lines of code. A collection like the argument `word` can be destructured into its different parts directly where it is declared as an argument. This is best illustrated with an example:

```clojure
guess-the-word.core=> (defn destruct [[one two & more]] (println "one is" one "; two is" two "and more is" more))
#'guess-the-word.core/destruct
guess-the-word.core=> (destruct [1 2 3 4])
one is 1 ; two is 2 and more is (3 4)
nil
guess-the-word.core=> (destruct "amazing")
one is a ; two is m and more is (a z i n g)
nil
```

Notice how the function `destruct` takes only one argument, but this argument is decomposed into 3 parts. When called with a collection, the first element of the collection becomes `one`, the second becomes `two` and the rest becomes the collection `more`.

By the way, what type of collection is `[1 2 3 4]`? This is called a vector and you've already seen it many times because the vector is the data structure that is used to declare the argument of a function. When using destructuring, we use another vector inside the vector of arguments.

Can you now improve the `reveal-letters` function by using destructuring?

<div class="solution closed"></div>
```clojure
(defn reveal-letters [[letter & more] known-letters]
  (let [current-letter (reveal-letter letter known-letters)]
    (if more
      (str current-letter (reveal-letters more known-letters))
      current-letter)))
```

# Playing the game

The function `play` will be the heart of our game. It will take 2 arguments, the secret word to guess and a set of letters already played. It will reveal the letters already played from the secret word and print out the result. If the result matches the secret (i.e. all its letters are revealed) then it will print out a congratulations message, otherwise it will ask for the next letter from the player and repeat the process. It should also print out the played letters to help the player keep track of them.

The only building block that we're missing is a small function `next-letter` that captures a letter from the user and adds it into a set, to be used like this:

```clojure
guess-the-word.core=> (next-letter #{})
Enter your next letter:r
#{\r}
```

Try writing this function and check the solution below.

<div class="solution closed"></div>
```clojure
(defn next-letter [letters]
  (println "Enter your next letter:")
  (conj letters (first (read-line))))
```

Now can you write the function `play`? To help you, here is an example of how it should behave:

```clojure
guess-the-word.core=> (play "test" #{})

Mystery word: ____
Letters played: #{}
Enter your next letter:t

Mystery word: t__t
Letters played: #{t}
Enter your next letter:e

Mystery word: te_t
Letters played: #{e t}
Enter your next letter:s

Mystery word: test
Letters played: #{e s t}
Congrats, you won!
nil
```

Try to implement this function on your own without peeking at the solution.

<div class="solution closed"></div>
```clojure
(defn play[secret letters]
  (let [revealed (reveal-letters secret letters)]
    (println "")
    (println "Mystery word:" revealed)
    (println "Letters played:" letters)
    (if (= revealed secret)
        (println "Congrats, you won!")
        (play secret (next-letter letters)))))
```

# Improving the display of letters

One small improvement we can make is to show only the letters played, without the set notation, meaning showing `est` instead of `#{e s t}` to make sure we don't confuse a player who doesn't know Clojure.

Let's observe with the REPL what we need to do:

```clojure
guess-the-word.core=> (println #{\a \b \c})
#{a b c}
nil
guess-the-word.core=> (str #{\a \b \c})
"#{\\a \\b \\c}"
guess-the-word.core=> (str \a \b \c)
"abc"
```

The last output is exactly what we want, but it requires to call the function `str` with all the elements extracted from the set. In other words, we want the elements of the set to become arguments of the function `str`. Clojure offers a function called `apply` to do exactly that:

```clojure
guess-the-word.core=> (apply str #{\a \b \c})
"abc"
```

Since those are individual letters, it would be nice to interpose a space between each letter. Fortunately Clojure offers a function called exactly that:

```clojure
guess-the-word.core=> (interpose \space #{\a \b \c})
(\a \space \b \space \c)
```

Can you improve the display of letters in the function `play` by using `apply` and `interpose`?

<div class="solution closed"></div>
```clojure
(defn play[secret letters]
  (let [revealed (reveal-letters secret letters)]
    (println "")
    (println "Mystery word:" revealed)
    (println "Letters played:" (apply str (interpose \space letters)))
    (if (= revealed secret)
        (println "Congrats, you won!")
        (play secret (next-letter letters)))))
```

# Keeping track of the missed letters

Since the letters that belong to the word are already displayed with the word itself, we could display only the missed letters in the next line, those that do not belong to the secret word.

Clojure offers a convenient function `difference` that returns the elements from a set that are not present in another one. However this function belongs to a different namespace named `clojure.set`, so we need to tell our program to load the functions from this namespace. This is done at the top of our file, in the `ns` form, like this:

```clojure
(ns guess-the-word.core
  (:require [clojure.set :as set])
  (:gen-class))
```

The part `:as set` is optional but it's a good practice to provide a shortcut to call functions from this namespace. After adding this new namespace, reload the file and you should be able to do this in the REPL:

```clojure
guess-the-word.core=> (set/difference #{1 2 3 4} #{2 4 6})
#{1 3}
```

Can you now write a function `missed-letters` that returns the letters of a set that don't belong to a word, like this:

```clojure
guess-the-word.core=> (missed-letters "apple" #{\a \b \c})
#{\b \c}
```

You'll need to convert the word into a set of its letters, we saw how to do that at the beginning of this project. Also this function `missed-letters` will be called from the function `play` so make sure that you write it above it.

<div class="solution closed"></div>
```clojure
(defn missed-letters [secret letters]
  (set/difference letters (into #{} secret)))
```

Can you know update the function play to display only the missed letters?

<div class="solution closed"></div>
```clojure
(defn play[secret letters]
  (let [revealed (reveal-letters secret letters)
        missed (missed-letters secret letters)]
    (println "")
    (println "Mystery word:" revealed)
    (println "Missed letters:" (apply str (interpose \space missed)))
    (if (= revealed secret)
        (println "Congrats, you won!")
        (play secret (next-letter letters)))))
```

Finally, we need to make the game a bit harder, so let's add a maximum number of misses, for example 7 or whatever you deem an appropriate level of difficulty. We also need to tell the player that they lost if they missed too many letters and tell them how many missed letters they have left. Ready to make these changes? Hint: you'll need the functions `-` and `count`.

<div class="solution closed"></div>
```clojure
(def max-misses 7)

(defn play[secret letters]
  (let [revealed (reveal-letters secret letters)
        missed (missed-letters secret letters)
        remaining (- max-misses (count missed))]
    (println "")
    (println "Mystery word:" revealed)
    (println "Missed letters:" (apply str (interpose \space missed)) "-" remaining "left")
    (cond
      (= revealed secret) (println "Congrats, you won!")
      (= 0 remaining) (println "Sorry, you lost, the word was" secret)
      :default (play secret (next-letter letters)))))
```

# Feeding random words to the game

We're almost done this game, the only part missing is to get the mystery word randomly from a list of words. For this, we will just leverage lists of words that people have published online. Pick your favorite from the following URLs (web addresses):

Countries:
<https://gist.githubusercontent.com/kalinchernev/486393efcca01623b18d/raw/daa24c9fea66afb7d68f8d69f0c4b8eeb9406e83/countries>

Animals:
<https://gist.githubusercontent.com/atduskgreg/3cf8ef48cb0d29cf151bedad81553a54/raw/82f142562cf50b0f6fb8010f890b2f934093553e/animals.txt>

1000 Common words:
<https://gist.githubusercontent.com/deekayen/4148741/raw/01c6252ccc5b5fb307c1bb899c95989a8a284616/1-1000.txt>

US cities
<https://gist.githubusercontent.com/norcal82/4accc0d968444859b408/raw/d295d94608be043a4774087d6cb19caf3a1f27a2/city_names.txt>

Note that their format are all the same, a simple text file with one word per line. Clojure offers a very convenient function called `slurp` to read the content of a web page, try it in the REPL with one of the addresses above, pretty easy right?

```clojure
guess-the-word.core=> (slurp "https://gist.githubusercontent.com/kalinchernev/486393efcca01623b18d/raw/daa24c9fea66afb7d68f8d69f0c4b8eeb9406e83/countries")
"Afghanistan\nAlbania\nAlgeria\nAndorra\nAngola\nAntigua & Deps\nArgentina\nArmenia\nAustralia\nAustria\nAzerbaijan\nBahamas\nBahrain\nBangladesh\nBarbados\nBelarus\nBelgium\nBelize\nBenin\nBhutan\nBolivia\nBosnia Herzegovina\nBotswana\nBrazil\nBrunei\nBulgaria\nBurkina\nBurundi\nCambodia\nCameroon\nCanada\nCape Verde\nCentral African Rep\nChad\nChile\nChina\nColombia\nComoros\nCongo\nCongo {Democratic Rep}\nCosta Rica\nCroatia\nCuba\nCyprus\nCzech Republic\nDenmark\nDjibouti\nDominica\nDominican Republic\nEast Timor\nEcuador\nEgypt\nEl Salvador\nEquatorial Guinea\nEritrea\nEstonia\nEthiopia\nFiji\nFinland\nFrance\nGabon\nGambia\nGeorgia\nGermany\nGhana\nGreece\nGrenada\nGuatemala\nGuinea\nGuinea-Bissau\nGuyana\nHaiti\nHonduras\nHungary\nIceland\nIndia\nIndonesia\nIran\nIraq\nIreland {Republic}\nIsrael\nItaly\nIvory Coast\nJamaica\nJapan\nJordan\nKazakhstan\nKenya\nKiribati\nKorea North\nKorea South\nKosovo\nKuwait\nKyrgyzstan\nLaos\nLatvia\nLebanon\nLesotho\nLiberia\nLibya\nLiechtenstein\nLithuania\nLuxembourg\nMacedonia\nMadagascar\nMalawi\nMalaysia\nMaldives\nMali\nMalta\nMarshall Islands\nMauritania\nMauritius\nMexico\nMicronesia\nMoldova\nMonaco\nMongolia\nMontenegro\nMorocco\nMozambique\nMyanmar, {Burma}\nNamibia\nNauru\nNepal\nNetherlands\nNew Zealand\nNicaragua\nNiger\nNigeria\nNorway\nOman\nPakistan\nPalau\nPanama\nPapua New Guinea\nParaguay\nPeru\nPhilippines\nPoland\nPortugal\nQatar\nRomania\nRussian Federation\nRwanda\nSt Kitts & Nevis\nSt Lucia\nSaint Vincent & the Grenadines\nSamoa\nSan Marino\nSao Tome & Principe\nSaudi Arabia\nSenegal\nSerbia\nSeychelles\nSierra Leone\nSingapore\nSlovakia\nSlovenia\nSolomon Islands\nSomalia\nSouth Africa\nSouth Sudan\nSpain\nSri Lanka\nSudan\nSuriname\nSwaziland\nSweden\nSwitzerland\nSyria\nTaiwan\nTajikistan\nTanzania\nThailand\nTogo\nTonga\nTrinidad & Tobago\nTunisia\nTurkey\nTurkmenistan\nTuvalu\nUganda\nUkraine\nUnited Arab Emirates\nUnited Kingdom\nUnited States\nUruguay\nUzbekistan\nVanuatu\nVatican City\nVenezuela\nVietnam\nYemen\nZambia\nZimbabwe"
```

Notice how each country is now separated by `\n`, this is actually the representation for a line break in a string format.

```clojure
guess-the-word.core=> (println "multiple\nlines")
multiple
lines
nil
```

Instead of doing `(println "")` in our play function, we could simply add `\n` before `"Mystery word"`.

But back to our list of words. The next thing that we need is to split these lines of text into a collection of words. We can use the function `split-lines` for this but it's in the namespace `clojure.string` so we'll need to import this new namespace. Also let's give the name `words` to this collection. Ready to try?

<div class="solution closed"></div>
```clojure
(ns guess-the-word.core
  (:require [clojure.set :as set]
            [clojure.string :as string])
  (:gen-class))

(def words
  (string/split-lines
    (slurp "https://gist.githubusercontent.com/kalinchernev/486393efcca01623b18d/raw/daa24c9fea66afb7d68f8d69f0c4b8eeb9406e83/countries")))
```

```clojure
guess-the-word.core=> (first words)
"Afghanistan"
```

Next we need to convert these words into lower case. The `clojure.string` namespace has a convenient function `lower-case` to do that but we'll need to apply it to all the words of our collection. Clojure offers the function `map` to apply a function to all the elements of a collection, here is how it works:

```clojure
guess-the-word.core=> (map string/lower-case ["A" "B"])
("a" "b")
```

Can you modify your code to convert all the words to lower case?

> ![Parinfer]({{ "/assets/caution.png" | absolute_url }}) If you sometimes struggle with Nightcode adding parentheses automatically, it's often just a matter of adding spaces at the beginning of the lines for the parentheses to get moved to the right place. Nightcode is using an editing mode called parinfer and you can see and interact with some demo here to see how it works: <https://shaunlebron.github.io/parinfer/>.

Are you done with your words?

<div class="solution closed"></div>
```clojure
(def words
 (map string/lower-case
   (string/split-lines
     (slurp "https://gist.githubusercontent.com/kalinchernev/486393efcca01623b18d/raw/daa24c9fea66afb7d68f8d69f0c4b8eeb9406e83/countries"))))
```

```clojure
guess-the-word.core=> (first words)
"afghanistan"
```

Perfect! Let's just learn a new clojure trick to make this code more readable. You probably realized by now that clojure code needs to be read inside-out to understand what it does and it can sometimes get complicated when there are too many nested expression. In order to improve the readability of this kind of code, clojure offers threading macros `->` and `->>` which allow building pipeline of expressions whose result feed into the next one either as the first argument with `->`, or as the last argument with `->>`. Here are some examples:

```clojure
guess-the-word.core=> (first (string/upper-case "hello"))
\H
guess-the-word.core=> (-> "hello" (string/upper-case) (first))
\H
guess-the-word.core=> (-> "hello" string/upper-case first)
\H
guess-the-word.core=> (-> 1 (+ 2) (+ 3))
6
guess-the-word.core=> (-> 1 (- 2) (- 3))
-4 ; same as (- (- 1 2) 3)
guess-the-word.core=> (->> 1 (- 2) (- 3))
2 ; same as (- 3 (- 2 1))
```

Note how the parentheses become optional when a function doesn't take any argument other than the result of the previous expression. That's why the first two examples are identical.

Can you use a threading macro to improve our words?

<div class="solution closed"></div>
```clojure
(def words
  (->> "https://gist.githubusercontent.com/kalinchernev/486393efcca01623b18d/raw/daa24c9fea66afb7d68f8d69f0c4b8eeb9406e83/countries"
       slurp
       string/split-lines
       (map string/lower-case)))
```

And finally can you write the function `-main` to play with a random word from the list? Remember that we saw a useful function in Project 2 to pick a random element from a collection.

<div class="solution closed"></div>
```clojure
(defn -main []
  (play (rand-nth words) #{}))
```

And now just have fun guessing those countries or other words.
