---
layout: post
title:  "Project 4 - Bouncing ball"
---

# The project

This project will show how you can use a graphics library to draw and animate things. The output will be a simple ball bouncing around the sides of a window.

![Demo]({{ "/assets/project4.gif" | absolute_url }})

# Using an open source library

Let's start by creating a new project, and this time pick `Graphics Project` in the Start menu. Give your project the name you want, for this article we'll use simply the name `ball`.

The main difference between this new project and the previous ones is found in `project.clj`, this file should look something like that:

```clojure
(defproject ball "0.0.1-SNAPSHOT"
  :description "FIXME: write description"
  :url "http://example.com/FIXME"
  :dependencies [[org.clojure/clojure "1.8.0"]
                 [quil "2.6.0" :exclusions [org.clojure/clojure]]]
  :main ball.core)
```

In previous projects, the only thing mentioned in `:dependencies` was Clojure itself. For this graphical project, we will use a library called Quil, you can check its web site at <http://quil.info/>.

Every significant programming project relies on libraries created by other people and published on the internet for everyone else to use for free, generally with some conditions like disclosing where the code is coming from. This is a huge time saver and opens up a world of possibilities. Clojure itself is an open source library, Quil is built on top of Clojure but also on top of Processing which provides all the visual rendering that we're going to use.

One slightly complicated thing when using open source libraries is versioning. When you intend to publish and maintain a real project, you should always try to use the latest stable version of every component that you use. For Clojure, you can see what the latest version is at <https://clojure.org/community/downloads>, for Quil, you can check <https://github.com/quil/quil/blob/master/RELEASE-NOTES.md>.

Nightcode itself is open source and the version you're using might already differ from the one I used when writing this tutorial. In my case, I see that I can upgrade the dependencies of my project to be `[org.clojure/clojure "1.9.0"]` and `[quil "2.7.1" :exclusions [org.clojure/clojure]]`. By the way, by using the `:exclusions` keyword, we ensure that we control the version of Clojure that we want to use and don't automatically inherit the one that Quil depends on. We could do the same things with other dependencies of Quil but it's not needed because we don't use these dependencies directly, and worse, things might break if we upgrade libraries randomly. You can see what other libraries Quil depends on by looking at its own `project.clj` file at <https://github.com/quil/quil/blob/master/project.clj>.

Phew, that was a lot of information about open source libraries. If you want, let's take a break and watch this little video that explains the origin and philosophy behind the open source movement:

<iframe width="720" height="405" src="https://www.youtube.com/embed/a8fHgx9mE5U" frameborder="0" allow="autoplay; encrypted-media" allowfullscreen></iframe>

The open source movement has been one of the most impactful innovation of all time. This is what allows us to write amazing programs with just a few lines of code of our own, by leveraging millions of lines of code written by other programmers.

# Discovering Quil

Let's now take a look at the `core.clj` file.

We saw in the previous project how to use functions from another namespace, so you should already be a bit familiar with the `ns` expression and its `:require` part. What we haven't done before is the `:refer :all` part. This means that all the functions from the `quil.core` namespace are immediately available.

In order to get a better sense of what is going on and what functions belong to quil, let's replace this with `(:require [quil.core :as quil])`. Using an explicit prefix is actually a recommended practice in Clojure for this reason.

Obviously things will break as calls to quil's functions now need to be prefixed with `quil/`. Can you find all 5 places that need this prefix? If you're not sure, just try running the program and you'll see errors like `Unable to resolve symbol: smooth in this context` that will point you in the right direction. When you're done, you should have a program that looks like the following:

<div class="solution closed"></div>
```clojure
(ns ball.core
  (:require [quil.core :as quil]))

(defn setup []
  (quil/smooth))

(defn draw []
  (quil/background 255)
  (quil/fill 192)
  (quil/ellipse 100 100 30 30))

(quil/defsketch example
  :title "Example"
  :setup setup
  :draw draw
  :size [200 200])

(defn -main [& args])
```

If you run this program, you will see a small window opening, showing a grey ball with a black border on a white background.

The main entry point into Quil is `defsketch`. It points to a setup function that is invoked once at the beginning and set some configuration, in this example, it only calls `smooth` to make the drawing look a bit better. Then it gives a pointer to the `draw` function which is actually called 60 times per second. This is configurable and we could add `(frame-rate 30)` inside the setup function to make it become 30 times per second, but the default should be just fine. Finally, it gives the size of the window in pixels.

The `draw` function calls 3 different quil functions to actually draw the small circle. You can refer to the documentation at <http://quil.info/api> to see the full list of functions available (there are a lot!) and get the documentation on how they work. Both `background` and `fill` functions use RGB colors as arguments, you can give them 3 numbers between 0 and 255 to provide the Red Green and Blue components of the color you want to use. When only 1 number is provided, it is used for all 3 components. Finally the `ellipse` function draws an ellipse at the given x and y coordinates and with the provided width and height, this becomes a circle when those 2 are the same.

Every time you reload the whole file, the program will create a new window. You can close it and reload the file again to avoid having too many, this will be needed if you change something inside `defsketch` like the size for instance. But if you change the `draw` function, you can select it and choose `Reload Selection` for the change to take effect in the already open window.

You will need to use `Reload Selection` a lot, so now is a good time to become familiar with some useful keyboard shortcuts. First, something that works with most text editors, use the arrows to put your cursor just before the function `draw`, then maintain the key `shift` pressed and press the down arrow as many times as you have lines to select (you can do the same from the bottom with `shift+Up`). Then press the `Ctrl` key on Windows (or `Command` on Mac) and Nightcode will display available shortcuts, you can see that `Reload Selection` is accessible with capital R, so press `Ctrl+shift+R` will reload the selection.

Go ahead and play with the various elements to change their color or size. You may want to resize and move your Nightcode window and the visual output window in order to display them side by side. Alternatively, you may want to use the keyboard shortcut `ALT+TAB` to navigate quickly between the code and the rendering.

# Dealing with errors

Because the `draw` function is called many times per second, any error that happens in it could look overwhelming. Let's spend a bit of time to see how to troubleshoot effectively these kinds of errors.

Imagine you want to change the color of the ball to be red, the proper code for this is to replace the call to the `fill` function with `(quil/fill 255 0 0)`. Try that, then select the entire `draw` function and reload your selection, you should see the ball become red. Let's say you also want to change the border to be green, you need to add a new line that calls `(quil/stroke 0 255 0)` before drawing the ellipse and reload the draw function again. So far so good? If you're already in trouble, keep reading to see how you can get out of it.

Let's introduce an error for demonstration purpose. Imagine that you finally decide that you don't want a border on your ball. The proper way to do that is to replace `(quil/stroke 0 255 0)` with `(quil/no-stroke)`. However, imagine that in your hurry, you forget to remove the numbers and the code becomes `(quil/no-stroke 0 255 0)`. Try this and brace yourself for impact, the REPL console will start getting flooded with the same error repeating over and over, which doesn't leave you any time to read what the problem is.

In that case, when you're reasonably sure what line is causing the problem, you can simply change it back to what you know was working before. `CTRL+Z` to undo may come in handy for that. You can also comment out the line that you suspect is wrong by adding a semi-colon in front, like this `;(quil/no-stroke 0 255 0)`. Once you're ready to try again, simply select the draw function again and reload the selection. If the error disappears, you can now scroll back in the console and see what the problem was. Try to go back all the way to the top of the error, it's usually contains the root cause of the problem while the other lines mention all the lines of code that are executed when the problem happens. In this case, you should be able to find something like that:

```console
Exception in  :draw  function:  #error {
 :cause Wrong number of args (3) passed to: core/no-stroke
```

If you get stuck and are not able to make the error go away, there is no reason to panic, you can simply click the button `Stop` to stop your program and you will also be able to read the content of the errors. The only difference is that you will need to restart your program to try again.

Finally, it's worth noting that sometimes the error is detected by the Clojure compiler, so the code is not even executed and the error is only displayed once in the console. Try for example misspelling a function name, like `(quil/nostroke)`, and you should see a `CompilerException` happening, like this:

```console
CompilerException java.lang.RuntimeException: No such var: quil/nostroke,
```

# Animating the ball

You've probably guessed that, since the draw function is called in rapid fire, slightly changing the x and y coordinates of the ball between each call would make the ball "move". In order to do that, we need to replace the hard coded 100 with a managed state.

Clojure offers a simple way to store a value in memory in a way that it can be changed later by the program, this is done with the function `atom` which can take any value we want as an argument. We could create two of these "atoms", one for the value x and one for the value y, but for this kind of application it's usually a good practice to use a single atom containing the entire state. This way, the atom can ensure that both x and y are modified together, or "atomically".

What kind of data structure could we put into this atom then? An associative data structure that we have discovered in project 2, do you remember? If so, try creating your `state` atom at the top of the file, otherwise just check the solution.

<div class="solution closed"></div>
```console
(def state (atom {:x 15
                  :y 15}))
```

The values for x and y are set so that the ball starts in the top left corner. If we wanted to change the size of the ball, we would need to change this starting position too. To make these dimensions proportional to each other, it's a good time to extract the ball radius into a constant with a new `def`.

<div class="solution closed"></div>
```console
(def ball-radius 15)

(def state (atom {:x ball-radius
                  :y ball-radius}))

(defn draw []
  (quil/background 255)
  (quil/fill 255 0 0)
  (quil/no-stroke)
  (quil/ellipse 100 100 (* 2 ball-radius) (* 2 ball-radius)))
```

The next step is to replace the hard coded 100 with these x and y coordinates from the state. In order to read the value of an atom, we could use the function `deref` as in `(deref state)`, however this is such a common operation that Clojure offers a shortcut for this with the `@` sign, as in `@state`. With this knowledge in mind, try replacing the 100 with x and y from the state.

<div class="solution closed"></div>
```console
(def ball-radius 15)

(def state (atom {:x ball-radius
                  :y ball-radius}))

(defn draw []
  (quil/background 255)
  (quil/fill 255 0 0)
  (quil/no-stroke)
  (quil/ellipse (:x @state) (:y @state) (* 2 ball-radius) (* 2 ball-radius)))
```

Finally, in order to make the ball move, we just need to change x and y after drawing the ball, so that the next time the function `draw` is called, it is using a slightly different value for x and y. In order to do that, we need to learn how to change values in a map and how to change a value in an atom.

The main functions used to change values in a map are `assoc` which associate new values to given keys, and `update` which applies a function to an existing value to change it. Let's illustrate with the REPL.

```clojure
ball.core=> (assoc {} :a 1 :b 2)
{:a 1, :b 2}
ball.core=> (assoc {:a 1 :b 2} :c 3)
{:a 1, :b 2, :c 3}
ball.core=> (update {:a 1 :b 2} :b inc)
{:a 1, :b 3}
```

Now in order to update an atom, we also have 2 main options: the functions `swap!` which will transform the existing value by applying a function to it, and the function `reset!` which will just blindly replace the atom value. Let's show these in a REPL:

```clojure
ball.core=> (def a (atom 10))
#'ball.core/a
ball.core=> @a
10
ball.core=> (swap! a inc)
11
ball.core=> @a
11
ball.core=> (reset! a 2)
2
ball.core=> @a
2
```

By the way, the function `inc` simply increments a number by one as you probably guessed. When you don't know a function, we saw in our very first project that you can look it up in the documentation. You can also quickly look up some documentation in the REPL with the function `doc`.

```clojure
ball.core=> (doc inc)
-------------------------
clojure.core/inc
([x])
  Returns a number one greater than num. Does not auto-promote
  longs, will throw on overflow. See also: inc'
nil
```

Now, let's write a function `move` that will increment the coordinate x and y of our ball. There are multiple ways to write this function using a combination of the functions we just saw. Ready to try it?

<div class="solution closed"></div>
```clojure
(defn move []
  (let [x (inc (:x @state))
        y (inc (:y @state))]
    (swap! state assoc :x x :y y)))

(defn draw []
  (quil/background 255)
  (quil/fill 255 0 0)
  (quil/no-stroke)
  (quil/ellipse (:x @state) (:y @state) (* 2 ball-radius) (* 2 ball-radius))
  (move))
```

After reloading the code, you should see your ball moving until it leaves the window. A fun thing is that you can easily read and change the state of the program from the REPL to bring the ball back into the window.

```clojure
ball.core=> @state
{:x 511, :y 511}
ball.core=> (reset! state {:x 0 :y 0})
{:x 0, :y 0}
```

# Make it bounce

Deciding to increment x and y by 1 on every refresh was very much arbitrary and made the ball go in a single direction. Instead we can 2 more variables into our state to determine the direction and speed of our ball, let's call them `move-x` and `move-y` and initialize them at 1 to produce the exact same result we just observed. Ready? Go!

<div class="solution closed"></div>
```clojure
(def state (atom {:x ball-radius
                  :y ball-radius
                  :move-x 1
                  :move-y 1}))

(defn move []
  (let [x (+ (:x @state) (:move-x @state))
        y (+ (:y @state) (:move-y @state))]
    (swap! state assoc :x x :y y)))
```

This `move` function now contains a lot of repetition of `deref` of state. Even with the shortcut `@state`, it is not very elegant and probably not very efficient either. We could deref the state only once and give names to each value but that would still be very verbose, unless we use destructuring. In Project 3, we saw how to destructure a vector but it's also possible to destructure a map and have all its values bound to names in one step. It's better explained with an example, so here we go, you can play along in the REPL (tip: use the up arrow to recall the last expression):

```clojure
ball.core=> (let [{:keys [a b] :as m} {:a 1 :b 2 :c 3}] a)
1
ball.core=> (let [{:keys [a b] :as m} {:a 1 :b 2 :c 3}] b)
2
ball.core=> (let [{:keys [a b] :as m} {:a 1 :b 2 :c 3}] m)
{:a 1, :b 2, :c 3}
```

In this example `{:a 1 :b 2 :c 3}` is the map that we want to destructure, `:keys` followed by a vector of names allow to bind values from the map to the names corresponding to their keys (this works only if the key is a keyword) and `:as` allows to retain the full map bound to a name as well. Can you use map destructuring to improve our `move` function?

<div class="solution closed"></div>
```clojure
(defn move []
  (let [{:keys [x y move-x move-y]} @state
        x (+ x move-x)
        y (+ y move-y)]
    (swap! state assoc :x x :y y)))  
```

Now, we just need to change `move-y` become -1 when the ball hits the bottom of the window, then back to 1 again when it hits the top. Similarly, we need to change `move-x` to become -1 when the ball hits the right border of the window, and 1 again when it hits the left border. This is a bigger exercise than we've done so far but you already have all the knowledge to do it. So give it a try. Okay, here are a few hints just in case, remember where we defined the width and height of the window? You'll probably want to extract those values into constants, like we did with `ball-radius`. Secondly, how do you know when the hits the bottom? It depends on 3 values: the x position, the ball radius and the height of the window. Thirdly, to test the multiple cases that change the direction of the ball, you can use `cond` like we did in previous projects or you can use `or` (e.g. `(if (or this that) then else)`).

Now that should be enough for you to do the work. Remember to start small, and doing when thing at a time before trying the program again, to make sure you don't break anything. The solution below will contain the full program of the bouncing ball. Worth noting that if you keep the window square, with the ball starting in one corner, then it will only bounce from corner to corner.

<div class="solution closed"></div>
```clojure
(ns ball.core
  (:require [quil.core :as quil]))

(def width 300)
(def height 200)
(def ball-radius 15)

(def state (atom {:x ball-radius
                  :y ball-radius
                  :move-x 1
                  :move-y 1}))

(defn move []
  (let [{:keys [x y move-x move-y]} @state
        x (+ x move-x)
        y (+ y move-y)
        move-x (if
                 (or (= x (- width ball-radius))
                     (= x ball-radius))
                 (- move-x)
                 move-x)
        move-y (if
                 (or (= y (- height ball-radius))
                     (= y ball-radius))
                 (- move-y)
                 move-y)]
    (swap! state assoc :x x :y y :move-x move-x :move-y move-y)))

(defn draw []
  (quil/background 255)
  (quil/fill 255 0 0)
  (quil/no-stroke)
  (quil/ellipse (:x @state) (:y @state) (* 2 ball-radius) (* 2 ball-radius))
  (move))

(defn setup []
  (quil/smooth))

(quil/defsketch example
  :title "Example"
  :setup setup
  :draw draw
  :size [width height])

(defn -main [& args])
```

You may have come up with a solution similar to the one above. If so, good job! This is really well done! But it's not over yet, once we have it working, it's time to think can we make it better? You may notice that the `move` function contains some repetition of code that looks almost the same. When you see something like that, it means you can most likely extract another function.

<div class="solution closed"></div>
```clojure
(defn adjust-direction [position direction max]
 (if
   (or (= position (- max ball-radius))
       (= position ball-radius))
   (if (< 0 direction) - direction)
   direction))

(defn move []
  (let [{:keys [x y move-x move-y]} @state
        x (+ x move-x)
        y (+ y move-y)
        move-x (adjust-direction x move-x width)
        move-y (adjust-direction y move-y height)]
    (swap! state assoc :x x :y y :move-x move-x :move-y move-y)))
```

# Bonus exercise

Make the ball accelerate every time it hits a border, so instead of going from 1 to -1, it goes from 1 to -2, then from -2 to 3, then from 3 to -4 and so on. Watch out, in the previous code, since the ball was moving 1 pixel at a time, it eventually hit the border. With the acceleration, it can zoom past the border and you have to account for this.

<div class="solution closed"></div>
```clojure
(defn adjust-direction [position direction max]
 (if
   (or (>= position (- max ball-radius))
       (<= position ball-radius))
   (if (< 0 direction) (- 0 1 direction) (- 0 -1 direction))
   direction))
```
